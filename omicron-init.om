# polymorphic functions
def id = ~A:*{^x:A{x}};
def const = ~A:*{~B:*{^x:A{^y:B{x}}}};

# fixed point combinator
def delta = ~A:*{~B:*{
  ^f:(A->B)->(A->B){^x:Rec(^T:*{T->(A->B)}){
    f(^y:A{unfold[Rec(^T:*{T->(A->B)})](x)(x)(y)})
  }}
}};
def fix = ~A:*{~B:*{
  ^f:(A->B)->(A->B){
    delta[A][B](f)(fold[Rec(^T:*{T->(A->B)})](delta[A][B](f)))
  }
}};

# hungry function
# unfold[Hungry](..(unfold[Hungry](hungry_func(0)))(1)))..) : Hungry
type def Hungry = Rec(^T:*{Int->T});
def hungry_func = fix[Int][Hungry](
  ^hungry_func:Int->Hungry{
    ^x:Int{
      fold[Hungry](hungry_func)
    }
  }
);

# Monad
type def Monad = ^M:*->*{${
  fmap:@A:*{@B:*{(A->B)->M(A)->M(B)}},
  pure:@A:*{A->M(A)},
  join:@A:*{M(M(A))->M(A)}
}};
def bind = ~M:*->*{~A:*{~B:*{
  ^monad:Monad(M){^m:M(A){^f:A->M(B){
    monad.join[B](monad.fmap[A][M(B)](f)(m))
  }}}
}}};

# Maybe
type def Maybe = ^A:*{@B:*{(A->B)->B->B}};
def just = ~A:*{
  ^x:A{
    ~B:*{^f:A->B{^g:B{f(x)}}}
  }
};
def nothing = ~A:*{
  ~B:*{^f:A->B{^g:B{g}}}
};
def maybe_monad = ${
  fmap=~A:*{~B:*{
    ^f:A->B{^m:Maybe(A){
      m[Maybe(B)](^x:A{just[B](f(x))})(nothing[B])
    }}
  }},
  pure=just,
  join=~A:*{
    ^m1:Maybe(Maybe(A)){
      m1[Maybe(A)](^m2:Maybe(A){m2})(nothing[A])
    }
  }
};

# List
type def List = ^A:*{Rec(^LA:*{@B:*{(A->LA->B)->B->B}})};
def cons = ~A:*{
  ^x:A{^xs:List(A){
    fold[List(A)](~B:*{^f:A->List(A)->B{^g:B{f(x)(xs)}}})
  }}
};
def nil = ~A:*{
  fold[List(A)](~B:*{^f:A->List(A)->B{^g:B{g}}})
};
def map = ~A:*{~B:*{
  fix[A->B][List(A)->List(B)](
    ^map:(A->B)->List(A)->List(B){
      ^f:A->B{^lst:List(A){
        unfold[List(A)](lst)[List(B)]
          (^x:A{^xs:List(A){cons[B](f(x))(map(f)(xs))}})
          (nil[B])
      }}
    }
  )
}};
def foldr = ~A:*{~B:*{
  fix[A->B->B][B->List(A)->B](
    ^foldr:(A->B->B)->B->List(A)->B{
      ^f:A->B->B{^z:B{^lst:List(A){
        unfold[List(A)](lst)[B]
          (^x:A{^xs:List(A){f(x)(foldr(f)(z)(xs))}})
          (z)
      }}}
    }
  )
}};
def append = ~A:*{
  fix[List(A)][List(A)->List(A)](
    ^append:List(A)->List(A)->List(A){
      ^lst1:List(A){^lst2:List(A){
        unfold[List(A)](lst1)[List(A)]
          (^x:A{^xs:List(A){cons[A](x)(append(xs)(lst2))}})
          (lst2)
      }}
    }
  )
};
def concat = ~A:*{foldr[List(A)][List(A)](append[A])(nil[A])};
def list_monad = ${
  fmap = map,
  pure = ~A:*{^x:A{cons[A](x)(nil[A])}},
  join = concat
};
